plugins {
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "java"
    id "maven-publish"
    id "java-library"
}

java {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
}

test {
  useJUnitPlatform()
}

ext.mainClass = "net.prominic.groovyls.GroovyLanguageServer"

group = 'com.apexsoft.lsp'
//下一个版本
//version = '2.5.1-RC4'
version = '1.0.0-SNAPSHOT'

//tasks.build.dependsOn tasks.shadowJar

shadowJar {
  manifest {
    attributes "Main-Class": "net.prominic.groovyls.GroovyLanguageServer"
  }
}
shadowJar.enabled=false
repositories  {
  mavenCentral()
}

dependencies {
  api "org.eclipse.lsp4j:org.eclipse.lsp4j:0.12.0"
  api "org.eclipse.lsp4j:org.eclipse.lsp4j.jsonrpc:0.12.0"
  api "org.apache.groovy:groovy:4.0.5"
  api "com.google.code.gson:gson:2.8.7"
  api "io.github.classgraph:classgraph:4.8.138"
  testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.2"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.7.2"
}

// assuming that Moonshine-IDE is checked out in a folder that has the same
// parent folder as groovy-language-server, copies the built .jar file to the
// appropriate location
task deployMoonshine(type: Copy) {
    dependsOn(build)
    from "build/libs/groovy-language-server-all.jar"
    into "../Moonshine-IDE/ide/MoonshineSharedCore/src/elements/groovy-language-server/"
}
task deployMoonshineTest(type: Copy) {
    dependsOn(build)
    from "build/libs/groovy-language-server-all.jar"
    into "../Moonshine-IDE/ide/MoonshineDESKTOPevolved/bin-debug/elements/groovy-language-server/"
}

publishing {
    publications {
        library(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            credentials {
                username = "${NEXUS_USER}"
                password = "${NEXUS_PASSWORD}"
            }
            if (project.version.endsWith('-SNAPSHOT')) {
                url "https://oss.apexsoft.com.cn/repository/maven-snapshots/"
            } else {
                url "https://oss.apexsoft.com.cn/repository/maven-releases/"
            }
        }
    }
}

